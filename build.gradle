//  Copyright 2019 Christian Schmitz
//  SPDX-License-Identifier: Apache-2.0

plugins {
    id 'com.android.library' version '4.0.2' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.4.10' apply false
    id 'org.jetbrains.dokka' version '0.10.1'
    id 'xyz.tynn.android.maven' version '0.1.0' apply false
    id 'xyz.tynn.jvm.kdoc' version '0.1.0' apply false
    id 'com.github.ben-manes.versions' version '0.33.0'
    id 'xyz.tynn.idea.fix' version '0.1.2'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    plugins.withType(org.jetbrains.dokka.gradle.DokkaPlugin) {
        if (project != rootProject)
            rootProject.dokka.subProjects += project.name

        tasks.withType(org.jetbrains.dokka.gradle.DokkaTask) {
            configuration {
                externalDocumentationLink {
                    url = new URL('https://developer.android.com/reference/kotlin/')
                    packageListUrl = new URL(url, '../androidx/package-list')
                }
                externalDocumentationLink {
                    url = new URL('https://kotlin.github.io/kotlinx.coroutines/kotlinx-coroutines-core/')
                    packageListUrl = new URL(url, 'package-list')
                }
            }
        }
    }
}

subprojects {
    group = 'xyz.tynn.butikk'

    tasks.withType(Test) {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }

    plugins.withId('org.jetbrains.kotlin.jvm') {
        project.apply plugin: 'xyz.tynn.jvm.kdoc'
        project.apply plugin: 'maven-publish'
        project.apply plugin: 'jacoco'

        java {
            withSourcesJar()
            withKdocJar()
        }

        publishing {
            publications {
                kotlinJvm(MavenPublication) {
                    from components.java
                }
            }
        }
    }

    plugins.withId('com.android.library') {
        project.apply plugin: 'xyz.tynn.android.kdoc'
        project.apply plugin: 'xyz.tynn.android.sources'
        project.apply plugin: 'xyz.tynn.android.maven'
    }

    plugins.withType(com.android.build.gradle.BasePlugin) {
        project.apply plugin: 'org.jetbrains.kotlin.android'

        android {
            compileSdkVersion 29
            buildToolsVersion '29.0.3'

            defaultConfig {
                minSdkVersion 14
                targetSdkVersion 29
                testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
            }

            compileOptions {
                sourceCompatibility '1.8'
                targetCompatibility '1.8'
            }

            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }

    plugins.withType(JacocoPlugin) {
        jacoco.toolVersion = '0.8.6'

        tasks.withType(JacocoReport) {
            check.dependsOn it
            reports.xml.enabled true
        }
    }

    plugins.withType(MavenPublishPlugin) {
        project.apply from: "$rootDir/publish.gradle"
    }
}

dependencyUpdates {
    def isNonStable = { String version ->
        ['alpha', 'beta', 'dev', 'rc', 'cr', 'm', 'preview', 'b', 'ea', 'eap'].any {
            version ==~ /(?i).*[.-]${it}[.\d-+]*.*/
        }
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
    gradleReleaseChannel = 'current'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
